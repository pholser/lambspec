buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'net.researchgate:gradle-release:2.0.0'
  }
}

def javaProjects() {
  return subprojects.findAll { new File(it.projectDir, 'src').exists() }
}

allprojects {
  apply plugin: 'base'
  apply plugin: 'maven'
  apply plugin: 'signing'
  apply plugin: 'release'
}

release {
  versionPropertyFile = '.version'
}

configure(javaProjects()) {
  apply plugin: 'java'

  group = 'com.pholser'
  version = file("${rootDir}/.version").text.split('=')[1].trim()
  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenCentral()
  }

  dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'com.google.guava', name: 'guava', version: '18.+'
  }

  tasks.withType(Jar) {
    baseName = "lambspec-$project.name"
    archivesBaseName = "lambspec-$project.name"
  }

  task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
  }

  task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives javadocJar, sourcesJar
  }

  signing {
    sign configurations.archives
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name "lambspec-$project.name"
          packaging 'jar'

          // optionally artifactId can be defined here
          description 'Assertion library for Java >= 8'
          url 'https://github.com/pholser/lambspec'

          scm {
            connection 'scm:git:git://github.com/pholser/lambspec.git'
            developerConnection 'scm:git:ssh://git@github.com/pholser/lambspec.git'
            url 'https://github.com/pholser/lambspec'
          }

          licenses {
            license {
              name 'The MIT License'
              url 'http://opensource.org/licenses/MIT'
            }
          }

          developers {
            developer {
              id 'pholser'
              name 'Paul Holser'
              email 'pholser@alumni.rice.edu'
            }
          }
        }
      }
    }
  }
}

project(':adapters:guava') {
  dependencies {
    compile project(':core')
    compile group: 'com.google.guava', name: 'guava', version: '18.+'
  }
}

project(':adapters:hamcrest') {
  dependencies {
    compile project(':core')
    compile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3+'
  }
}

project(':junit') {
  dependencies {
    compile group: 'junit', name: 'junit', version: '4.12'
  }
}
