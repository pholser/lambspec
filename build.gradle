def javaProjects() {
    return subprojects.findAll { new File(it.projectDir, 'src').exists() }
}

allprojects {
    apply plugin: 'base'
}

configure(javaProjects()) {
    apply plugin: 'java'

    group = 'com.pholser'
    version = '0.1'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'com.google.guava', name: 'guava', version: '18.+'
    }

    tasks.withType(Jar) {
        baseName = "lambspec-$project.name"
    }
}

project(':adapters:guava') {
    dependencies {
        compile project(':core')
        compile group: 'com.google.guava', name: 'guava', version: '18.+'
    }
}

project(':adapters:hamcrest') {
    dependencies {
        compile project(':core')
        compile group: 'com.google.guava', name: 'guava', version: '18.+'
        compile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3+'
    }
}

project(':junit') {
    dependencies {
        compile group: 'junit', name: 'junit', version: '4.12'
    }
}
